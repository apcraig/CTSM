#! /bin/csh -f

if !(-d $CASEBUILD/ctsmconf) mkdir -p $CASEBUILD/ctsmconf

#--------------------------------------------------------------------
# Invoke ctsm configure - output will go in CASEBUILD/ctsmconf
#--------------------------------------------------------------------

set config_opts=" "
if ($LND_GRID == "reg" && $GRID != "CTSM_USRDAT" ) then
   set config_opts=" -sitespf_pt $GRID"
endif
if ("$CCSM_COMPSET" =~ P* || "$CCSM_COMPSET" =~ R* ) then
   set config_opts=" -sitespf_pt $LND_GRID"
endif

cd $CASEBUILD/ctsmconf  
$CODEROOT/lnd/ctsm/bld/configure  $config_opts -comp_intf $COMP_INTERFACE \
    $CTSM_CONFIG_OPTS -usr_src $CASEROOT/SourceMods/src.ctsm || exit -1 

#--------------------------------------------------------------------
# Create ctsm.buildnml.csh
#--------------------------------------------------------------------

if ($RUN_TYPE == startup ) then
   if ($CTSM_FORCE_COLDSTART == on) then
     set START_TYPE = "cold"
   else
     set START_TYPE = "default"
   endif
else
   if ($RUN_TYPE == hybrid ) then
     set START_TYPE = "startup"
   else
     set START_TYPE = $RUN_TYPE
   endif
endif

set RESOLUTION = $LND_GRID
set ctsmusr     = ""
if ($LND_GRID == reg ) then
   if ( $GRID == CTSM_USRDAT ) then
      set RESOLUTION = $CTSM_USRDAT_NAME
      set ctsmusr     = " -ctsm_usr_name $CTSM_USRDAT_NAME"
   else
      set RESOLUTION = $GRID
   endif
endif

set default_lnd_in_filename = "lnd_in"

set inst_counter = 1
while ($inst_counter <= $NINST_LND)

if ($NINST_LND > 1) then
   set inst_string = $inst_counter
   if ($inst_counter <= 999) set inst_string = "0$inst_string"
   if ($inst_counter <=  99) set inst_string = "0$inst_string"
   if ($inst_counter <=   9) set inst_string = "0$inst_string"
   set inst_string = "_${inst_string}"    
else
   set inst_string = ""       
endif
set lnd_in_filename = ${default_lnd_in_filename}${inst_string}

setenv INST_STRING $inst_string

cd $CASEBUILD/ctsmconf  

if (-e $CASEBUILD/ctsm.input_data_list) rm $CASEBUILD/ctsm.input_data_list

if (-e $CASEROOT/user_nl_ctsm${inst_string}) then
  $UTILROOT/Tools/user_nlcreate -user_nl_file $CASEROOT/user_nl_ctsm${inst_string} \
    -namelist_name ctsm_inparm >! $CASEBUILD/ctsmconf/cesm_namelist  || exit -2
endif

set glc_opts = ""
if ("$COMP_GLC" != "sglc" )then
   set glc_opts = "-glc_grid $GLC_GRID -glc_smb .$GLC_SMB. "
endif

set usecase = " "
if ($CTSM_NML_USE_CASE != "UNSET") set usecase = "-use_case $CTSM_NML_USE_CASE"

set ctsm_startfile = " "
if ( $RUN_TYPE == "hybrid" || $RUN_TYPE == "branch" ) then
   set ctsm_startfile = "-ctsm_startfile ${RUN_REFCASE}.ctsm2.r.${RUN_REFDATE}-${RUN_REFTOD}.nc"
endif

$CODEROOT/lnd/ctsm/bld/build-namelist -infile $CASEBUILD/ctsmconf/cesm_namelist \
    -csmdata $DIN_LOC_ROOT  \
    -inputdata $CASEBUILD/ctsm.input_data_list \
    -namelist "&clm_inparm $CTSM_NAMELIST_OPTS /" $usecase $glc_opts \
    -res $RESOLUTION $ctsmusr -clm_start_type $START_TYPE $ctsm_startfile \
    -l_ncpl $LND_NCPL -lnd_frac "${LND_DOMAIN_PATH}/${LND_DOMAIN_FILE}" \
    -glc_nec $GLC_NEC -co2_ppmv $CCSM_CO2_PPMV -co2_type $CTSM_CO2_TYPE \
    -config $CASEBUILD/ctsmconf/config_cache.xml $CTSM_BLDNML_OPTS || exit -3
    
if (-d ${RUNDIR}) then
  cp $CASEBUILD/ctsmconf/lnd_in ${RUNDIR}/$lnd_in_filename || exit -2
  # Only copy drv_flds_in namelist file if one doesn't already exist
  if ( ! -f "${RUNDIR}/drv_flds_in" ) cp $CASEBUILD/ctsmconf/drv_flds_in ${RUNDIR}/. >& /dev/null
endif

@ inst_counter = $inst_counter + 1

end


